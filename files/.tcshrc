# prompt
if ($?TERM) then
  switch ($TERM)
    case "xterm*":
      set prompt="%{\033]0;%n@%m\007%}{%!} %m:%~% "
      breaksw
    default:
      set prompt="{%!} %m:%~% "
      breaksw
  endsw
endif

umask 077
unset autologout
set autolist set color set colorcat
set nobeep

# shortcuts
alias ls "ls -aF"
alias cvs '\cvs -d $CVSROOT \!*'
alias stat "cvs status | egrep '^File|^\?'"
alias statn "cvs status | egrep '^File|^\?' | grep -v 'Up-to-date'"
if (-e /usr/local/bin/nvi || -e /usr/bin/nvi) then
  alias vi nvi
endif

# keys
bindkey ^U load-average
bindkey ^[[1~ beginning-of-line
bindkey ^[[4~ end-of-line

# handy
complete cd p/1/d/ # cd only to directories

# env
setenv CVS_RSH "/usr/bin/ssh"
setenv EDITOR "vi"
setenv IRCNAME "*Unknown*"
setenv GOROOT ~/src/go
setenv PATH ~/bin:/bin:/sbin:/usr/bin:/usr/sbin:/usr/local/bin:/usr/local/sbin:/usr/X11R6/bin:/usr/ccs/bin:/usr/ucb:/usr/local/share/python:/home/glewis/src/go/bin:/usr/local/go/bin
setenv VISUAL "vi"
setenv TMPDIR ~/tmp

# os-specific stuff
if ($?OSTYPE) then
  switch ($OSTYPE)
    case "darwin":
      alias lock '/System/Library/Frameworks/ScreenSaver.framework/Resources/ScreenSaverEngine.app/Contents/MacOS/ScreenSaverEngine'
      alias airport '/System/Library/PrivateFrameworks/Apple80211.framework/Versions/Current/Resources/airport -I'
      setenv JAVA_HOME /System/Library/Frameworks/JavaVM.framework/Home/
      if(! -e ~/Music/iTunes) then
        ln -s /dev/null ~/Music/iTunes
      endif
      if(-d ~/Pictures) then
        echo "removing Pictures folder..."
        sudo mv ~/Pictures ~/Pictures.bad
        ln -s /dev/null ~/Pictures
      endif
      breaksw
    case "solaris":
      alias grep ggrep
      alias tar gtar
      breaksw
    case "linux":
      synclient TouchPadOff=1
      alias ls "ls -aF --color"
    default:
      breaksw
  endsw
endif

# clean up annoying defaults
foreach FILE (~/.bash_history ~/.bash_logout ~/.bashrc ~/.asterisk_history \
    ~/.mysql_history ~/.psql_history)
  if (-e $FILE) then
    rm $FILE
  endif
  ln -s /dev/null $FILE
end

# create common directories, link from desktop if appropriate
foreach DIR (pub src bin tmp keys projects)
  if (! -e ~/$DIR) then
    mkdir ~/$DIR
  endif
  if (-e ~/Desktop) then
    if (! -e ~/Desktop/$DIR) then
      ln -s ~/$DIR ~/Desktop/$DIR
     endif
  endif
end

# keychain
if (-e /usr/local/bin/keychain || -e /usr/bin/keychain) then
  if (-f ~/.keychain/local-csh) then
    source ~/.keychain/local-csh
  endif
  if (-f ~/.keychain/local-csh-gpg) then
    source ~/.keychain/local-csh-gpg
  endif
endif

# fonts
if (-e ~/.fonts) then
  cd ~/.fonts
  mkfontdir .
  xset fp+ ~/.fonts
  xset fp rehash
  cd ~
endif

# local
if (-f ~/.tcshrc.local) then
   source ~/.tcshrc.local
endif
